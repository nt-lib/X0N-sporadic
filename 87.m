//////////////////////////87//////////////


load "models_and_maps.m";
load "rank_0_auxiliary.m";

// These are files from the paper N. Ad≈æaga, T. Keller, P. Michaud-Jacobs, F. Najman, E. Ozman, B. Vukorepa, Computing quadratic points on modular curves X_0(N), Math. Comp. 93 (2024), 1371-1397. We use some auxilliary code, e.g. for computing the j-map, from there.

gensfp := function(Xp,divs)
//This function computes the set of generators of divisor classes supported on divisors in divs. It also returns the invariants of the generated group.
P0:=divs[1];
C,phi,psi:=ClassGroup(Xp);
Z:=FreeAbelianGroup(1);
degr:=hom<C->Z | [ Degree(phi(a))*Z.1 : a in OrderedGenerators(C)]>;
A:=Kernel(degr);
divs0:=[psi(D-Degree(D)*P0) : D in divs];
Ksub1:=sub<A | divs0>;
r:=#pls;
inds:=[i : i in [1..r]];
i:=r+1;
repeat
	i:=i-1;
	indsNew:=Exclude(inds,i);
	if sub<A | [divs0[j] : j in indsNew]> eq Ksub1 then
		inds:=indsNew;
	end if;
until i eq 1;
divsNew:=[divs0[j] : j in inds];
r:=#divsNew;	
Zr:=FreeAbelianGroup(r);
h:=hom<Zr->A | divsNew>;
Ksub:=Image(h);
assert Ksub eq Ksub1;
hf:=hom<Zr->Ksub | [ Ksub!h(Zr.i) :  i in [1..r] ]>;
bas:=[Eltseq(i@@hf) : i in OrderedGenerators(Ksub)];
divgen:=[phi(&+[d[i]*(divsNew[i]) : i in [1..#divsNew]]) : d in bas ];
return divgen, Invariants(Ksub);
end function;


procedure RRCheck(k, bounds, current, A, P0,~counter)
//This is a recursive function that runs through all the divisor classes in Pic^3 generated by divisor classes in A and counts the number contianing an effective divisor of degree 3. 
    if #current eq k then
        // Access the element at the current index tuple
        elem := 0*A[1];
        for i in [1..#current] do
			k:=current[i];
            elem := elem+(k*A[i]); 
        end for;

        // Check a condition
		D:=elem+3*P0;
        if IsEffective(D) then
			counter:=counter+1;
		else
			L,eta:=RiemannRochSpace(D);
			if Dimension(L) gt 0 then
				counter:=counter+1;
			end if;
		end if;
        return;
    end if;

    idx := #current + 1;
    for i in [1..bounds[idx]] do
        RRCheck(k, bounds, current cat [i], A,P0, ~counter);
    end for;
end procedure;


  

N:=87;
p:=5;
X, _, _, _, cusp := eqs_quos(N, []);
Xp:=ChangeRing(X,GF(5));
T:=[[1 , 0 , 0 , 0 , 0 , 1 , 0 , 1 , 0],[-1 , 0 , 0 , 0 , 0 , 1 , 0 , 1 , 0], [1 , 0 , 0 , 0 , 0 , 1 , 0 , -1 , 0], [-1 , 0 , 0 , 0 , 0 , 1 , 0 , 1 , 0] ];
pts := [ Xp ! a : a in T];
pls := [ Place(a) : a in pts];
gens,invs:=gensfp(Xp,pls);
counter:=0;
current:=[];
RRCheck(#invs, invs, current, gens, pls[1], ~counter);
assert counter eq (Binomial(#pts+3-1,3)); //We prove that the number of effective degree 3 divisors over F_p is the same as the number of of cuspidal divisors, completing the proof.












